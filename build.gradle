import org.gradle.api.plugins.internal.JvmPluginsHelper
import com.github.glassmc.kiln.standard.CustomTransformer
import com.github.glassmc.kiln.standard.environment.*
import org.objectweb.asm.*
import org.objectweb.asm.tree.*

buildscript {
    repositories {
        mavenCentral()

        maven {
            url = 'https://glassmc.ml/repository/'
        }
        maven {
            url = 'https://jitpack.io/'
        }
    }

    dependencies {
        classpath 'com.github.glassmc:kiln:0.8.0'
    }
}

apply plugin: 'java'
apply plugin: 'kiln-main'
apply plugin: 'maven-publish'

group = "com.github.glassmc"
version = "1.0"

JvmPluginsHelper.addApiToSourceSet(sourceSets.main, project.configurations)

sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url = 'https://glassmc.ml/repository/'
    }
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    implementation 'com.github.glassmc:loader:0.9.6'

    api 'commons-io:commons-io:2.11.0'

    api 'org.ow2.asm:asm-tree:9.2'
    api 'org.ow2.asm:asm-commons:9.2'
    api 'org.ow2.asm:asm-util:9.2'
    api 'com.google.guava:guava:31.1-jre'
    api 'org.spongepowered:mixin:0.8.5'

    compileOnly 'net.minecraft:client-1.8.9:yarn'
}

kiln {
    environment = new GlassLoaderEnvironment("0.9.6")
    transformers = [new MixinRemapper()]
}

class MixinRemapper extends CustomTransformer {

    private final Map<String, String> mixinClasses = new HashMap<>()
    private Map<String, ClassNode> classNodes

    @Override
    void map(List<ClassNode> context, Map<String, ClassNode> classNodes) {
        if (classNodes.isEmpty()) {
            return
        }

        this.classNodes = classNodes

        for (ClassNode classNode : context) {
            for (AnnotationNode annotationNode : classNode.invisibleAnnotations) {
                if (annotationNode.desc = "Lorg/spongepowered/asm/mixin/Mixin;") {
                    if (annotationNode.values != null) {
                        List<Type> values = annotationNode.values.get(annotationNode.values.indexOf("value") + 1) as List<Type>
                        if (values.get(0) instanceof Type) {
                            mixinClasses.put(classNode.name, values.get(0).className.replace(".", "/"))
                        }
                    }
                }
            }
        }

        for(String className : classNodes.keySet()) {
            ClassNode classNode = classNodes.get(className)

            for (AnnotationNode annotationNode : classNode.invisibleAnnotations) {
                if (annotationNode.desc = "Lorg/spongepowered/asm/mixin/Mixin;") {
                    if (annotationNode.values != null) {
                        List<Type> values = annotationNode.values.get(annotationNode.values.indexOf("value") + 1) as List<Type>
                        if (values.get(0) instanceof Type) {
                            List<Type> oldValues = new ArrayList<>(values)
                            values.clear()

                            for (Type type : oldValues) {
                                values.add(Type.getType("L" + this.getRemapper().map(type.getClassName().replace(".", "/")) + ";"))
                            }
                        }
                    }
                }
            }

            for(FieldNode fieldNode : classNode.fields) {
                if(this.getMixinClass(className) != null) {
                    for(AnnotationNode annotationNode : fieldNode.visibleAnnotations) {
                        if(annotationNode.desc == "Lorg/spongepowered/asm/mixin/Shadow;") {
                            fieldNode.name = this.mapFieldName(className, fieldNode.name, fieldNode.desc)
                        }
                    }
                }
            }

            for(MethodNode methodNode : classNode.methods) {

                if(this.getMixinClass(className) != null) {
                    for(AnnotationNode annotationNode : methodNode.visibleAnnotations) {
                        if(annotationNode.desc == "Lorg/spongepowered/asm/mixin/gen/Invoker;" ||
                                annotationNode.desc == "Lorg/spongepowered/asm/mixin/gen/Accessor;" ||
                                annotationNode.desc == "Lorg/spongepowered/asm/mixin/Overwrite;" ||
                                annotationNode.desc == "Lorg/spongepowered/asm/mixin/Shadow;") {
                            methodNode.name = this.mapMethodName(className, methodNode.name, methodNode.desc)
                        }
                    }

                    for(AnnotationNode annotationNode : methodNode.visibleAnnotations) {
                        if (annotationNode.desc == "Lorg/spongepowered/asm/mixin/gen/Accessor;") {
                            if (annotationNode.values != null) {
                                int index = annotationNode.values.indexOf("value")
                                String string = annotationNode.values.get(index + 1) as String

                                string = this.mapFieldName(className, string, "")

                                annotationNode.values.set(index + 1, string)
                            }
                        } else if (annotationNode.desc == "Lorg/spongepowered/asm/mixin/gen/Invoker;") {
                            if (annotationNode.values != null) {
                                int index = annotationNode.values.indexOf("value")
                                String string = (annotationNode.values.get(index + 1) as String)
                                int descIndex = string.indexOf("(")
                                String name = string.substring(0, descIndex)
                                String descriptor = string.substring(descIndex)

                                string = this.mapMethodName(className, name, descriptor) + this.getRemapper().mapDesc(descriptor)

                                annotationNode.values.set(index + 1, string)
                            }
                        }
                    }

                    for(AnnotationNode annotationNode : methodNode.visibleAnnotations) {
                        if(annotationNode.desc == "Lorg/spongepowered/asm/mixin/injection/Inject;" ||
                                annotationNode.desc == "Lorg/spongepowered/asm/mixin/injection/Redirect;" ||
                                annotationNode.desc == "Lorg/spongepowered/asm/mixin/injection/ModifyConstant;") {
                            List<String> targets = annotationNode.values.get(annotationNode.values.indexOf("method") + 1) as List<String>
                            List<String> newTargets = new ArrayList<>()
                            for(String string : targets) {
                                int splitIndex = string.indexOf('(')
                                String name = string.substring(0, splitIndex)
                                String desc = string.substring(splitIndex)
                                newTargets.add(this.getRemapper().mapMethodName(this.getMixinClass(className), name, desc) + this.getRemapper().mapMethodDesc(desc))
                            }
                            int index = annotationNode.values.indexOf(targets)
                            annotationNode.values.remove(targets)
                            annotationNode.values.add(index, newTargets)
                        }

                        if(annotationNode.values != null && annotationNode.values.contains("at")) {
                            AnnotationNode atAnnotation = annotationNode.values.get(annotationNode.values.indexOf("at") + 1) as AnnotationNode

                            if (atAnnotation.values.get(0) instanceof AnnotationNode) {
                                atAnnotation = atAnnotation.values.get(0) as AnnotationNode
                            }

                            if(atAnnotation.values.contains("target")) {
                                String target = atAnnotation.values.get(atAnnotation.values.indexOf("target") + 1)

                                int index = target.indexOf(";")
                                String[] classMethodSplit = [target.substring(0, index), target.substring(index + 1)]
                                String className1 = classMethodSplit[0].substring(1)
                                int methodDescIndex = classMethodSplit[1].indexOf('(')
                                if (methodDescIndex != -1) {
                                    String methodName = classMethodSplit[1].substring(0, methodDescIndex)
                                    String methodDesc = classMethodSplit[1].substring(methodDescIndex)

                                    int targetIndex = atAnnotation.values.indexOf(target)

                                    atAnnotation.values.remove(target)

                                    atAnnotation.values.add(targetIndex, "L" + this.getRemapper().map(className1) + ";" + this.getRemapper().mapMethodName(className1, methodName, methodDesc) + this.getRemapper().mapMethodDesc(methodDesc))
                                } else {
                                    if (classMethodSplit[1].contains(":")) {
                                        String[] fieldSplit = classMethodSplit[1].split(":")

                                        String fieldName = fieldSplit[0]
                                        int targetIndex = atAnnotation.values.indexOf(target)

                                        atAnnotation.values.remove(target)

                                        atAnnotation.values.add(targetIndex, "L" + this.getRemapper().map(className1) + ";" + this.getRemapper().mapFieldName(className1, fieldName, "") + ":" + this.getRemapper().mapDesc(fieldSplit[1]))
                                    } else {
                                        String fieldName = classMethodSplit[1]
                                        int targetIndex = atAnnotation.values.indexOf(target)

                                        atAnnotation.values.remove(target)

                                        atAnnotation.values.add(targetIndex, "L" + this.getRemapper().map(className1) + ";" + this.getRemapper().mapFieldName(className1, fieldName, ""))
                                    }
                                }
                            }
                        }
                    }
                }
            }
        }

        for (String className : classNodes.keySet()) {
            for (MethodNode methodNode : classNodes.get(className).methods) {
                for(AbstractInsnNode node : methodNode.instructions.toArray()) {
                    if(node instanceof FieldInsnNode) {
                        if(this.getMixinClass(node.owner) != null) {
                            node.name = this.mapFieldName(node.owner, node.name, node.desc)
                        }
                    }
                    if(node instanceof MethodInsnNode) {
                        if(this.getMixinClass(node.owner) != null) {
                            node.name = this.mapMethodName(node.owner, node.name, node.desc)
                        }
                    }
                }
            }
        }
    }

    private String mapMethodName(String className, String methodName, String methodDesc) {
        String mixinClass = this.getMixinClass(className)

        String methodName2 = this.getRemapper().mapMethodName(mixinClass, methodName, methodDesc)

        if (methodName2 != methodName) {
            return methodName2
        }

        if(methodName.startsWith("invoke") || methodName.startsWith("call")) {
            String prefix = methodName.startsWith("invoke") ? "invoke" : "call"
            String strippedName = methodName.replace(prefix, "")
            strippedName = strippedName.charAt(0).toLowerCase().toString() + strippedName.substring(1)
            methodName = this.getRemapper().mapMethodName(mixinClass, strippedName, methodDesc)
            methodName = prefix + methodName.charAt(0).toUpperCase().toString() + methodName.substring(1)
        } else if(methodName.startsWith("get") || methodName.startsWith("set") || methodName.startsWith("is")) {
            String prefix = methodName.startsWith("get") ? "get" : methodName.startsWith("set") ? "set" : "is"
            String strippedName = methodName.replace(prefix, "")
            strippedName = strippedName.charAt(0).toLowerCase().toString() + strippedName.substring(1)
            methodName = this.getRemapper().mapFieldName(mixinClass, strippedName, methodDesc)
            methodName = prefix + methodName.charAt(0).toUpperCase().toString() + methodName.substring(1)
        }

        return methodName
    }

    private String mapFieldName(String className, String fieldName, String fieldDesc) {
        String mixinClass = this.getMixinClass(className)
        return this.getRemapper().mapFieldName(mixinClass, fieldName, fieldDesc)
    }

    private String getMixinClass(String className) {
        return mixinClasses.get(className)
    }

}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}