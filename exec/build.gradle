plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'java-library'
apply plugin: 'kiln-main'

repositories {
    mavenCentral()
    maven {
        url = 'https://jitpack.io/'
    }
    maven {
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    resolvedApi
    api.extendsFrom(resolvedApi)
}

dependencies {
    for(Project shardProject : collectAllProjects(parent)) {
        if(shardProject.getBuildFile().exists() && shardProject != project) {
            String name = getName(shardProject)
            Project project = project(name.isEmpty() ? ':' : name)
            resolvedApi project
        }
    }
    implementation 'com.github.glassmc:loader:v0.4.0'
}

List<Project> collectAllProjects(Project project) {
    List<Project> childProjects = new ArrayList<>()
    childProjects.add(project)

    for(Project childProject : project.childProjects.values()) {
        childProjects.addAll(collectAllProjects(childProject))
    }

    return childProjects
}

String getName(Project project) {
    return project.parent != null ? getName(project.parent) + ':' + project.getName() : ''
}

shadowJar {
    dependencies {
        configurations = [project.configurations.resolvedApi]
        /*exclude(dependency {
            it.configuration != 'runtimeElements'
        })*/
    }
}